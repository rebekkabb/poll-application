plugins {
    id "base"
    id "com.github.node-gradle.node" version "2.2.0"
}

group 'as.ter.rebekka'
version '0.0.1-SNAPSHOT'

node {
    version = '12.13.1'
    download = true
}

repositories {
    mavenCentral()
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

task buildWebpack(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run', 'build']
    inputs.files project.fileTree(dir: 'src', include: "**/*")
    outputs.file 'dist/build.js'
}

task start(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run', 'start']
    outputs.upToDateWhen { false }
}

task packageNpmApp(type: Zip) {
    dependsOn buildWebpack
    group "build"
    baseName 'poll-frontend'
    extension 'jar'
    destinationDir file("${projectDir}/build")

    from('dist') {
        into 'static'
    }
}

artifacts {
    npmResources(packageNpmApp.archiveFile) {
        builtBy packageNpmApp
        type "jar"
    }
}

clean {
    delete packageNpmApp.archiveFile
}

assemble.dependsOn buildWebpack
assemble.dependsOn packageNpmApp
